// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * The bit-combinations that yield each function are 
 * documented in the book. In addition, the ALU 
 * computes two 1-bit outputs: if the ALU output
 * is 0, zr is set to 1; otherwise zr is set to 0;
 * If out<0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx==1) set x = 0        // 16-bit constant
// if (nx==1) set x = ~x       // bitwise "not"
// if (zy==1) set y = 0        // 16-bit constant
// if (ny==1) set y = ~y       // bitwise "not"
// if (f==1)  set out = x + y  // integer 2's complement addition
// if (f==0)  set out = x & y  // bitwise "and"
// if (no==1) set out = ~out   // bitwise "not"
// if (out==0) set zr = 1
// if (out<0) set ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if 1) or out = x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out==0), 0 otherwise
        ng; // 1 if (out<0),  0 otherwise

    PARTS:
   // Put you code here:
   // X
   Mux16(a=x, b=false, sel=zx, out=m1);
   Not16(in=m1, out=n1);
   Mux16(a=m1, b=n1, sel=nx, out=m2);
   // Y
   Mux16(a=y, b=false, sel=zy, out=m3);
   Not16(in=m3, out=n2);
   Mux16(a=m3, b=n2, sel=ny, out=m4);

   // Function
   And16(a=m2, b=m4, out=andout);
   Add16(a=m2, b=m4, out=addout);
   Mux16(a=andout, b=addout, sel=f, out=m5);
   // Whether to negate result
   Not16(in=m5, out=n3);
   Mux16(a=m5, b=n3, sel=no, out[0..7]=toor1, out[8..15]=toor2, out[15]=ng, out=out);

   // Output
   Or8Way(in=toor1, out=or1);
   Or8Way(in=toor2, out=or2);
   Or(a=or1, b=or2, out=or3);
   Not(in=or3, out=zr);

}

